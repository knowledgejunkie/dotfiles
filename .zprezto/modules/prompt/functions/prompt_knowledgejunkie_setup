#
# A theme based on Steve Losh's Extravagant Prompt and steeef with vcs_info
# integration.
#
# Authors:
#   Steve Losh <steve@stevelosh.com>
#   Bart Trojanowski <bart@jukie.net>
#   Brian Carper <brian@carper.ca>
#   steeef <steeef@gmail.com>
#   Sorin Ionescu <sorin.ionescu@gmail.com>
#   Nick Morrott <knowledgejunkie@gmail.com>
#
# Screenshots:
#   TODO
#

function prompt_knowledgejunkie_precmd {
  # Check for untracked files or updated submodules since vcs_info does not.
  if [[ -n $(git ls-files --other --exclude-standard 2> /dev/null) ]]; then
    branch_format="(${_prompt_knowledgejunkie_colors[1]}%b%f%u%c${_prompt_knowledgejunkie_colors[4]}●%f)"
  else
    branch_format="(${_prompt_knowledgejunkie_colors[1]}%b%f%u%c)"
  fi

  zstyle ':vcs_info:*:prompt:*' formats "${branch_format}"

  vcs_info 'prompt'

  if (( $+functions[python-info] )); then
    python-info
  fi
}

function prompt_knowledgejunkie_setup {
  setopt LOCAL_OPTIONS
  unsetopt XTRACE KSH_ARRAYS
  prompt_opts=(cr percent subst)

  # Load required functions.
  autoload -Uz add-zsh-hook
  autoload -Uz vcs_info

  # Add hook for calling vcs_info before each command.
  add-zsh-hook precmd prompt_knowledgejunkie_precmd

  # Use extended color palette if available.
  if [[ $TERM = *256color* || $TERM = *rxvt* ]]; then
    _prompt_knowledgejunkie_colors=(
      "%F{6}" # was Turquoise
      "%F{9}"  # was Orange
      "%F{13}" # was Purple
      "%F{1}"  # was Hotpink
      "%F{2}"  # was Limegreen
    )
  else
    _prompt_knowledgejunkie_colors=(
      "%F{cyan}"
      "%F{yellow}"
      "%F{magenta}"
      "%F{red}"
      "%F{green}"
    )
  fi

  # Formats:
  #   %b - branchname
  #   %u - unstagedstr (see below)
  #   %c - stagedstr (see below)
  #   %a - action (e.g. rebase-i)
  #   %R - repository path
  #   %S - path in the repository
  local branch_format="(${_prompt_knowledgejunkie_colors[1]}%b%f%u%c)"
  local action_format="(${_prompt_knowledgejunkie_colors[5]}%a%f)"
  local unstaged_format="${_prompt_knowledgejunkie_colors[2]}●%f"
  local staged_format="${_prompt_knowledgejunkie_colors[5]}●%f"

  # Set vcs_info parameters.
  zstyle ':vcs_info:*' enable bzr git hg svn
  zstyle ':vcs_info:*:prompt:*' check-for-changes true
  zstyle ':vcs_info:*:prompt:*' unstagedstr "${unstaged_format}"
  zstyle ':vcs_info:*:prompt:*' stagedstr "${staged_format}"
  zstyle ':vcs_info:*:prompt:*' actionformats "${branch_format}${action_format}"
  zstyle ':vcs_info:*:prompt:*' formats "${branch_format}"
  zstyle ':vcs_info:*:prompt:*' nvcsformats   ""

  # Set python-info parameters.
  zstyle ':prezto:module:python:info:virtualenv' format '(%v)'

  # Define prompts.
  PROMPT="
${_prompt_knowledgejunkie_colors[3]}%n%f on ${_prompt_knowledgejunkie_colors[2]}%m%f in ${_prompt_knowledgejunkie_colors[5]}%~%f "'${vcs_info_msg_0_}'"
"'$python_info[virtualenv]'"$ "
  SPROMPT='zsh: correct %F{red}%R%f to %F{green}%r%f [nyae]? '
}

prompt_knowledgejunkie_setup "$@"
